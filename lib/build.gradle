buildscript {
    repositories {
        google()
        jcenter()
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.0-alpha03'
    }
}

plugins {
    id 'org.jetbrains.kotlin.multiplatform' version '1.3.70'
    id("org.jetbrains.kotlin.native.cocoapods") version "1.3.70"
}
group 'com.example'
version '0.0.1'

apply plugin: 'maven-publish'
apply plugin: 'com.android.library'

ext {
    reaktiveVersion = '1.1.12'
}

android {
    compileSdkVersion 29
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    releaseApi "com.badoo.reaktive:reaktive-android-release:${reaktiveVersion}"
    debugApi "com.badoo.reaktive:reaktive-android-debug:${reaktiveVersion}"
}


kotlin {
    jvm()
    android()
    js {
        browser {
        }
        nodejs {
        }
    }
    // For ARM, should be changed to iosArm32 or iosArm64
    // For Linux, should be changed to e.g. linuxX64
    // For MacOS, should be changed to e.g. macosX64
    // For Windows, should be changed to e.g. mingwX64
    macosX64("macos")
    ios("ios")
    cocoapods {
        // Configure fields required by CocoaPods.
        summary = "Kedux is a kotlin-multiplatform state management library."
        homepage = "Link to a Kotlin/Native module homepage"
    }
    [targets.iosX64, targets.iosArm64].forEach {
        it.binaries
                .findAll { it instanceof org.jetbrains.kotlin.gradle.plugin.mpp.Framework }
                .every {
                    it.baseName = "Kedux"
                    it.freeCompilerArgs += "-Xobjc-generics"
                }
    }

    sourceSets {
        commonMain {
            dependencies {
                api kotlin('stdlib-common')
                api "com.badoo.reaktive:reaktive:${reaktiveVersion}"
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
                implementation "com.badoo.reaktive:reaktive-testing:${reaktiveVersion}"
            }
        }
        jvmMain {
            dependencies {
                api kotlin('stdlib-jdk8')
                api "com.badoo.reaktive:reaktive-jvm:${reaktiveVersion}"
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
                implementation "com.badoo.reaktive:reaktive-testing-jvm:${reaktiveVersion}"
            }
        }
        androidMain {
            dependencies {
                api kotlin('stdlib')
            }
        }
        androidTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
                implementation "com.badoo.reaktive:reaktive-testing-android:${reaktiveVersion}"
            }
        }
        jsMain {
            dependencies {
                api kotlin('stdlib-js')
                api "com.badoo.reaktive:reaktive-js:${reaktiveVersion}"
            }
        }
        jsTest {
            dependencies {
                implementation kotlin('test-js')
                implementation "com.badoo.reaktive:reaktive-testing-js:${reaktiveVersion}"
            }
        }
        macos {
            dependencies {
                api "com.badoo.reaktive:reaktive-macosx64:${reaktiveVersion}"
            }
        }
        macosTest {
            dependencies {
                implementation "com.badoo.reaktive:reaktive-testing-macosx64:${reaktiveVersion}"
            }
        }
        ios {
            dependencies {
                api "com.badoo.reaktive:reaktive-native:${reaktiveVersion}"
            }
        }
        iosTest {
            dependencies {
                implementation "com.badoo.reaktive:reaktive-testing-native:${reaktiveVersion}"
            }
        }
    }
}
